plugins {
    id 'org.springframework.boot' version '3.0.0-M3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id("maven-publish")
    id 'java'
}

group 'io.github.guqing'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "SonatypeSnapShotsPackages"
            url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = project.findProperty("ossr.user") ?: System.getenv("OSSR_USERNAME")
                password = project.findProperty("ossr.password") ?: System.getenv("OSSR_PASSWORD")
            }
        }
    }
    publications {
        ossr(MavenPublication) {
            from(components.java)
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    manifest {
        attributes "Implementation-Title": "Halo Pluggable Suite",
                "Implementation-Version": archiveVersion
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    implementation 'org.pf4j:pf4j:3.6.0'
    implementation "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    compileOnly 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}
